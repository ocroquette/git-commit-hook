/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.2.1/userguide/tutorial_java_projects.html
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    id 'groovy'
    id 'application'
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.atlassian.com/maven/repository/public"
    }
}

dependencies {
    implementation 'com.atlassian.jira:jira-rest-java-client-core:5.2.2'
    implementation 'com.atlassian.util.concurrent:atlassian-util-concurrent:3.0.0'
    implementation "org.slf4j:slf4j-simple:1.7.35"

    testImplementation gradleTestKit()
    testImplementation platform("org.spockframework:spock-bom:2.0-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-junit4"  // for old JUnit 4 rules
    testImplementation "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testImplementation "net.bytebuddy:byte-buddy:1.6.5" // allows mocking of classes (in addition to interfaces)
    testImplementation "org.objenesis:objenesis:2.5.1"  // allows mocking of classes without default constructor
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

def generatedResources = "$buildDir/generated-resources/main"
def pkgName = "com.github.ocroquette.commithook"
def pkgPath = pkgName.replace(".", "/")

sourceSets {
    main {
        output.dir(generatedResources, builtBy: 'generateResources')
    }
}

application {
    mainClass.set("com.github.ocroquette.commithook.Main")
}

task generateResources {
    dependsOn "generateProperties"
    group "build"
}

task generateProperties {
    description "Generate the property file with the configuration to access the Jira server"
    group "build"
    doLast {
        def propertyNames = [
                "jiraUrl",
                "jiraUser",
                "jiraPassword",
                "wrapLength"
        ]
        def propertyFile = file(generatedResources + "/" + pkgPath + "/settings.properties")
        def props = new Properties()
        def missingProperties = []
        propertyNames.each { propertyName ->
            // Read the project properties from Gradle and save them in $generatedResources/jira.properties
            if (project.hasProperty(propertyName))
                props.setProperty(propertyName, project.property(propertyName))
            else {
                props.setProperty(propertyName, "")
                missingProperties.add(propertyName)
            }
        }
        propertyFile.parentFile.mkdirs()
        propertyFile.withWriter { props.store(it, "Generated from project properties") }
        if (!missingProperties.isEmpty()) {
            project.logger.warn("WARNING: following project properties are not set for configuration: "
                    + missingProperties.join(", "))
        }
    }
}

def generateMD5(File file) {
    def digest = java.security.MessageDigest.getInstance("MD5")
    file.eachByte(4096) { buffer, length ->
        digest.update(buffer, 0, length)
    }
    new BigInteger(1, digest.digest()).toString(16).padLeft(32, '0')
}

task createShellScript {
    group "Custom"
    dependsOn "shadowJar"
    description "Create a shell script that contains the JAR file and extracts it on the first execution"
    doLast {
        // Print MD5 of JAR file on the console to have some kind of traceability
        def jarFile = new File("build/libs/git-commit-hook.jar")
        println "MD5 of JAR file: " + generateMD5(jarFile)

        def base64Jar = jarFile.bytes.encodeBase64().toString().split("(?<=\\G.{100})").join("\n")
        def outputJar = "commit-msg.jar"
        def outputTruststore = "truststore"
        // -Djavax.net.debug=all -Djavax.net.ssl.trustStorePassword=changeit
        def trustStoreSupport = project.hasProperty("trustStoreSupport") &&
                project.hasProperty("jiraUrl") &&
                project.property("jiraUrl").toLowerCase().startsWith("https")

        def cmdLine
        if(trustStoreSupport) {
            cmdLine = "java  -Djavax.net.ssl.trustStore=\"`dirname \$0`/$outputTruststore\" -jar \"`dirname \$0`/$outputJar\" \"\$@\""
        }
        else {
            cmdLine = "java  -jar \"`dirname \$0`/$outputJar\" \"\$@\""
        }

        def shellScript = """#!/bin/sh

JAR="`dirname \$0`/$outputJar"
echo "Extracting $outputJar"
awk '/^__BASE64_JAR_START__/ { P=1 ; next } /^__BASE64_JAR_END__/ { P=0 ; next } { if ( P==1 ) print \$0 }' \$0 | base64 --decode > "\$JAR"
"""
        if (trustStoreSupport) {
            shellScript += """
TRUSTSTORE="`dirname \$0`/$outputTruststore"
echo "Extracting $outputTruststore"
awk '/^__BASE64_TS_START__/ { P=1 ; next } /^__BASE64_TS_END__/ { P=0 ; next } { if ( P==1 ) print \$0 }' \$0 | base64 --decode > "\$TRUSTSTORE"
"""
        }
        shellScript += """
# Replace the extracting script by the starting script
(
    echo "#!/bin/sh"
    echo '$cmdLine'
) > "\$0.tmp"
mv "\$0.tmp" "\$0"
chmod +x "\$0"
$cmdLine

exit 0
"""
        shellScript += "__BASE64_JAR_START__\n$base64Jar\n__BASE64_JAR_END__\n"
        if(trustStoreSupport) {
            File truststore = new File("$buildDir/truststore")
            new CertificatesDownloader().downloadToTruststore(new URI(project.property("jiraUrl")).getHost(), 443, truststore)
            def base64Ts = truststore.bytes.encodeBase64().toString().split("(?<=\\G.{100})").join("\n")
            shellScript += "__BASE64_TS_START__\n$base64Ts\n__BASE64_TS_END__\n"
        }

        new File("build/commit-msg").text = shellScript
    }
}
